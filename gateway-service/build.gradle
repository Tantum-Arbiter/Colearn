plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.5'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco'
}

group = 'com'
version = '0.0.1-SNAPSHOT'
description = ''

jar {
	manifest {
		attributes(
				'Main-Class': 'com.app.service.GatewayServiceApplication'
		)
	}
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	// Avoid repo.maven.apache.org - causes 403 in Cloud Build
	maven {
		url 'https://maven-central-asia.storage-download.googleapis.com/maven2/'
		name 'GoogleAsiaProxy'
	}
	maven {
		url 'https://maven-central-eu.storage-download.googleapis.com/maven2/'
		name 'GoogleEUProxy'
	}
	google()
}

ext {
	set('springCloudVersion', "2025.0.0")
	set('nettyVersion', "4.1.129.Final")
	set('grpcVersion', "1.75.0")
	set('googleAuthVersion', "1.37.1")
}

// Force transitive dependency versions to fix CVEs and version mismatches
configurations.all {
	resolutionStrategy {
		force "io.grpc:grpc-netty-shaded:${grpcVersion}"
		force "io.netty:netty-handler:${nettyVersion}"
		force "io.netty:netty-codec-http:${nettyVersion}"
		force "io.netty:netty-common:${nettyVersion}"
		force "io.netty:netty-buffer:${nettyVersion}"
		force "io.netty:netty-transport:${nettyVersion}"
		force "io.netty:netty-codec:${nettyVersion}"
		force "io.netty:netty-resolver:${nettyVersion}"
		// Fix google-auth-library version mismatch (CertificateSourceUnavailableException)
		force "com.google.auth:google-auth-library-credentials:${googleAuthVersion}"
		force "com.google.auth:google-auth-library-oauth2-http:${googleAuthVersion}"
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-authorization-server'
	implementation 'org.springframework.cloud:spring-cloud-starter-gateway-server-webmvc'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-security'

	// JWT dependencies
	implementation 'com.auth0:java-jwt:4.4.0'
	implementation 'com.fasterxml.jackson.core:jackson-databind'

	// Firebase dependencies
	implementation 'com.google.firebase:firebase-admin:9.7.0'

	// Google Cloud Firestore (explicit for @PropertyName annotation)
	implementation 'com.google.cloud:google-cloud-firestore:3.27.2'

	// Google Cloud Storage for asset delivery
	implementation 'com.google.cloud:google-cloud-storage:2.43.1'

	// Google Auth - explicit versions to fix CertificateSourceUnavailableException
	implementation "com.google.auth:google-auth-library-oauth2-http:${googleAuthVersion}"
	implementation "com.google.auth:google-auth-library-credentials:${googleAuthVersion}"

	// Explicit dependencies to fix CVEs in firebase-admin transitive deps
	implementation "io.grpc:grpc-netty-shaded:${grpcVersion}"
	implementation "io.netty:netty-codec-http:${nettyVersion}"

	// Monitoring
	implementation 'io.micrometer:micrometer-registry-prometheus'
	implementation 'io.micrometer:micrometer-registry-stackdriver:1.12.0'

	// Resilience4j for circuit breakers, time limiters and metrics
	implementation 'io.github.resilience4j:resilience4j-spring-boot3:2.1.0'
	implementation 'io.github.resilience4j:resilience4j-micrometer:2.1.0'
	implementation 'io.github.resilience4j:resilience4j-timelimiter:2.1.0'

	// Testing
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'

	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
		html.required = true
	}
}